plugins {
    id 'kotlin-multiplatform' version '1.3.72'
    id "com.jfrog.bintray" version "1.8.4"
    id 'maven-publish'
    id 'jacoco'
    id 'org.jetbrains.dokka' version '0.10.1'
}

repositories {
    mavenCentral()
    jcenter()
}

group 'de.westermann'
version '1.0.0'

kotlin {
    jvm()
    js() {
        compilations.all {
            kotlinOptions {
                moduleKind = "umd"
                sourceMap = true
                metaInfo = true
            }
        }
    }

    sourceSets {
        commonMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
            }
        }
        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }
        jvmMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
            }
        }
        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }
        jsMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-js'
            }
        }
        jsTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-js'
            }
        }
    }
}

dokka {
    outputFormat = 'html'
    outputDirectory = "$buildDir/dokka"

    multiplatform {
        global {
            // perPackageOptions, sourceLinks and externalDocumentationLinks from here will be copied to every other platform (jvm and js in eg.)
            // perPackageOption {
            //     prefix = "de.westermann.kobserve"
            //     suppress = true
            // }
            // perPackageOption {
            //     prefix = "kotlin.internal"
            //     suppress = true
            // }
            perPackageOption {
                prefix = "de.westermann.kobserve"
                skipDeprecated = false
                reportUndocumented = true
                includeNonPublic = false
            }
            sourceLink {
                path = "src/commonMain/kotlin"
                url = "https://github.com/pixix4/KObserve/tree/master/src/commonMain/kotlin"
                lineSuffix = "#L"
            }
        }
        common {}
        jvm {}
        js {}
    }
}

jacoco {
    toolVersion = "0.8.5"
}

task jacocoTestReport(type: JacocoReport, dependsOn: jvmTest) {
    getAdditionalSourceDirs().from(files("$projectDir/src/commonMain/kotlin"))
    getAdditionalClassDirs().from(files("$buildDir/classes/kotlin/jvm/main"))
    reports {
        xml.enabled = true
    }
    getExecutionData().from(files("$buildDir/jacoco/jvmTest.exec"))
    afterEvaluate {
        getAdditionalSourceDirs().from(
                files(additionalClassDirs.files.collect {
                    fileTree(dir: it, exclude: 'de/westermann/kobserve/Extensions*')
                })
        )
    }
}

check.dependsOn jacocoTestReport

publishing {
    publications {
        jvm(MavenPublication) {
            pom {
                name = 'KObserve-jvm'
                description = 'An easy to use kotlin library for observable properties.'
                url = 'https://github.com/pixix4/KObserve'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/pixix4/KObserve/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'pixix4'
                        name = 'Lars Westermann'
                        email = 'lars-westermann@live.de'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/pixix4/KObserve.git'
                    developerConnection = 'scm:git:ssh://github.com/pixix4/KObserve.git'
                    url = 'https://github.com/pixix4/KObserve'
                }
            }
        }
        js(MavenPublication) {
            pom {
                name = 'KObserve-js'
                description = 'An easy to use kotlin library for observable properties.'
                url = 'https://github.com/pixix4/KObserve'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/pixix4/KObserve/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'pixix4'
                        name = 'Lars Westermann'
                        email = 'lars-westermann@live.de'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/pixix4/KObserve.git'
                    developerConnection = 'scm:git:ssh://github.com/pixix4/KObserve.git'
                    url = 'https://github.com/pixix4/KObserve'
                }
            }
        }
        metadata(MavenPublication) {
            pom {
                name = 'KObserve-metadata'
                description = 'An easy to use kotlin library for observable properties.'
                url = 'https://github.com/pixix4/KObserve'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/pixix4/KObserve/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'pixix4'
                        name = 'Lars Westermann'
                        email = 'lars-westermann@live.de'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/pixix4/KObserve.git'
                    developerConnection = 'scm:git:ssh://github.com/pixix4/KObserve.git'
                    url = 'https://github.com/pixix4/KObserve'
                }
            }
        }
        kotlinMultiplatform(MavenPublication) {
            pom {
                name = 'KObserve'
                description = 'An easy to use kotlin library for observable properties.'
                url = 'https://github.com/pixix4/KObserve'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/pixix4/KObserve/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'pixix4'
                        name = 'Lars Westermann'
                        email = 'lars-westermann@live.de'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/pixix4/KObserve.git'
                    developerConnection = 'scm:git:ssh://github.com/pixix4/KObserve.git'
                    url = 'https://github.com/pixix4/KObserve'
                }
            }
        }
    }
    repositories {
        maven {
            url = "$buildDir/repos/releases"
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')

    publications = ['jvm', 'js', 'metadata', 'kotlinMultiplatform']

    publish = true

    pkg {
        repo = 'maven'
        name = 'KObserve'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/pixix4/KObserve.git'

        version {
            name = project.version
            released = new Date()
        }
    }
}
